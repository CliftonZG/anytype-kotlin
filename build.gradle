apply from: './dependencies.gradle'

buildscript {
    ext.kotlin_version = '1.5.21'
    ext.gradle_tools = '3.1.3'
    ext.build_tools = '29.0.0'
    ext.nav_version = '2.3.0'
    ext.dokka_version = '1.4.32'

    ext.compile_sdk = 29
    ext.target_sdk = 29
    ext.min_sdk = 24

    ext.application_id = 'com.anytypeio.anytype'
    ext.version_name = '1.0'
    ext.version_code = 1
    ext.test_runner = 'androidx.test.runner.AndroidJUnitRunner'

    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$nav_version"
        classpath 'com.google.gms:google-services:4.3.8'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.7.1'
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
        classpath 'com.squareup.wire:wire-gradle-plugin:3.5.0'
    }
}

/**
 * Create github.properties in root project folder file
 * with gpr.usr=GITHUB_USER_ID  & gpr.key=PERSONAL_ACCESS_TOKEN
 */
def githubProperties = new Properties()
githubProperties.load(new FileInputStream(rootProject.file('github.properties')))

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/anytypeio/go-anytype-middleware")
            credentials {
                username = githubProperties['gpr.usr']
                password = githubProperties['gpr.key']
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task runUnitTests(dependsOn: [':app:testDebugUnitTest']) {
    description 'Run all unit tests'
}
